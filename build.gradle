apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "signing"

group = "com.github.marceloemanoel"
archivesBaseName="vraptor-js-controller"
version="0.2"

if(!hasProperty("sonatypeUsername")) {
    ext.sonatypeUsername = ""
}

if(!hasProperty("sonatypePassword")) {
    ext.sonatypePassword = ""
}

signing {
    sign configurations.archives
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}
 
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
 
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
 
            pom.project {
               name 'vraptor-js-controller'
               packaging 'jar'
               description 'vraptor-js-controller helps interaction through ajax'
               url 'https://github.com/marceloemanoel/vraptor-js-controller'
 
               scm {
                   url 'git@github.com:marceloemanoel/vraptor-js-controller.git'
                   connection 'scm:git@github.com:marceloemanoel/vraptor-js-controller.git'
                   developerConnection 'scm:git@github.com:marceloemanoel/vraptor-js-controller.git'
               }
 
               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }
 
               developers {
                   developer {
                       id 'marceloemanoel'
                       name 'Marcelo Emanoel B. Diniz'
                   }
               }
           }
        }
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

repositories{
  mavenCentral()
  mavenRepo url: "http://maven.ow2.org/maven2/"
}

def springModules = [
  "spring-core",
  "spring-context",
  "spring-context-support",
  "spring-aop",
  "spring-aspects",
  "spring-tx",
  "spring-beans",
  "spring-expression",
  "spring-jdbc",
  "spring-orm",
  "spring-oxm",
  "spring-web",
  "spring-asm",
  "spring-test"
]


dependencies {
  springModules.each {
    compile group:"org.springframework",   name: it, version:"3.1.0.RELEASE"
  }
  compile     group:"br.com.caelum"      , name: "vraptor" ,    version: "3.4.0"
  compile     group:"org.apache.velocity", name: "velocity",    version: "1.7"
  compile     group:"com.google.javascript",name:"closure-compiler", version:"r1810"
  compile     group:"javax.servlet"      , name: "servlet-api", version:"2.5"
  testCompile group:"org.mockito"        , name: "mockito-all", version:"1.9.0-rc1"
  testCompile group:"junit"              , name: "junit",       version:"4.8.2"
}

task wrapper(type:Wrapper) {
  gradleVersion = '1.3'
}

compileJava {
  options.debug = true
  options.debugOptions.debugLevel = "source,lines,vars"
  options.encoding = "UTF-8"
}
